apply plugin: 'jacoco'

jacoco {
    toolVersion "0.8.2"
    reportsDir = file("${buildDir}/reports/jacoco") // this is the default
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['createProdDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*',
                      '**/com.nytimes.model/**']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/prod/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testProdDebugUnitTest.exec",
            "outputs/code-coverage/connected/flavours/PROD/*coverage.ec"
    ])
}
